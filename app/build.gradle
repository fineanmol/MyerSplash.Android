apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'
apply plugin: 'kotlin-kapt'

android {
    signingConfigs {
        release {
            def properties = new Properties()
            properties.load(project.rootProject.file('local.properties').newDataInputStream())
            def key = properties.getProperty('key')
            def password = properties.getProperty('password')

            keyAlias key
            keyPassword password
            storeFile file('keystore/ms.jks')
            storePassword password
        }
    }

    compileSdkVersion 30

    defaultConfig {
        applicationId "com.juniperphoton.myersplash"
        minSdkVersion 21
        targetSdkVersion 29
        versionCode 626
        versionName "4.1.1"

        def properties = new Properties()
        properties.load(project.rootProject.file('local.properties').newDataInputStream())
        def unsplashKey = properties.getProperty('unsplashKey')
        def appCenterKey = properties.getProperty('appcenterKey')

        buildConfigField 'String', "UNSPLASH_APP_KEY", "\"" + unsplashKey + "\""
        buildConfigField 'String', "APP_CENTER_KEY", "\"" + appCenterKey + "\""

        javaCompileOptions {
            annotationProcessorOptions {
                arguments = ["room.schemaLocation": "$projectDir/schemas".toString()]
            }
        }

        ndk {
            abiFilters 'arm64-v8a', 'armeabi-v7a'
        }
        vectorDrawables {
            useSupportLibrary true
        }
    }

    buildFeatures {
        compose true
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    kotlinOptions {
        jvmTarget = "1.8"
        useIR = true
    }

    composeOptions {
        kotlinCompilerExtensionVersion '1.0.0-beta01'
    }

    buildTypes {
        debug {
            applicationIdSuffix ".debug"
            resValue "string", "authorities", "com.juniperphoton.myersplash.debug.fileProvider"
            manifestPlaceholders = [
                    "APP_NAME": "MyerSplash DEBUG"
            ]
            ndk {
                abiFilters 'arm64-v8a', 'armeabi-v7a', "x86"
            }
        }
        release {
            resValue "string", "authorities", "com.juniperphoton.myersplash.fileProvider"
            manifestPlaceholders = [
                    "APP_NAME": "MyerSplash"
            ]
            signingConfig signingConfigs.release
            shrinkResources false
            minifyEnabled false
        }
    }

    sourceSets {
        main.java.srcDirs += 'src/main/kotlin'
        main.res.srcDirs += 'src/debug'
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    composeOptions {
        kotlinCompilerExtensionVersion compose_version
        kotlinCompilerVersion '1.5.10'
    }

    packagingOptions {
        exclude 'META-INF/atomicfu.kotlin_module'
        resources {
            excludes += '/META-INF/{AL2.0,LGPL2.1}'
        }
    }
}

allprojects {
    configurations.all {
        resolutionStrategy {
            force 'org.xerial:sqlite-jdbc:3.34.0'
        }
    }
}

dependencies {
    implementation fileTree(include: ['*.aar'], dir: 'libs')
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlin_version"
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-core:$kotlin_coroutine_version"
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-android:$kotlin_coroutine_version"
    implementation 'com.jakewharton.retrofit:retrofit2-kotlin-coroutines-adapter:0.9.2'

    implementation "androidx.recyclerview:recyclerview:1.2.1"

    implementation "com.google.android.material:material:1.4.0"
    implementation "androidx.legacy:legacy-support-v13:$androidx_version"
    implementation "androidx.appcompat:appcompat:$appcompat"
    implementation "androidx.cardview:cardview:$androidx_version"
    implementation "androidx.browser:browser:1.3.0"
    implementation "androidx.palette:palette:$androidx_version"
    implementation 'androidx.constraintlayout:constraintlayout:1.1.3'

    implementation "androidx.compose.ui:ui:$compose_version"
    implementation "androidx.compose.material:material:$compose_version"
    implementation "androidx.compose.material:material-icons-extended:$compose_version"
    implementation "androidx.compose.ui:ui-tooling:$compose_version"
    implementation "androidx.compose.runtime:runtime-livedata:$compose_version"
    implementation 'androidx.lifecycle:lifecycle-viewmodel-compose:1.0.0-alpha07'
    implementation "androidx.compose.ui:ui-tooling-preview:$compose_version"
    implementation 'androidx.activity:activity-compose:1.3.0'
    implementation "androidx.activity:activity-ktx:1.3.0"
    androidTestImplementation "androidx.compose.ui:ui-test-junit4:$compose_version"

    implementation "com.jakewharton:butterknife:${butterknife_version}"
    kapt "com.jakewharton:butterknife-compiler:${butterknife_version}"

    implementation 'com.facebook.fresco:fresco:2.0.0'
    implementation "com.facebook.fresco:imagepipeline-okhttp3:2.0.0"

    implementation 'io.reactivex.rxjava2:rxjava:2.2.9'
    implementation 'io.reactivex.rxjava2:rxandroid:2.1.1'
    implementation 'com.squareup.retrofit2:retrofit:2.9.0'
    implementation 'com.squareup.retrofit2:converter-gson:2.3.0'
    implementation 'com.google.code.gson:gson:2.8.6'
    implementation 'com.google.android.flexbox:flexbox:3.0.0'

    implementation 'com.google.dagger:dagger:2.24'
    kapt 'com.google.dagger:dagger-compiler:2.24'

    def appCenterSdkVersion = '2.1.0'
    implementation "com.microsoft.appcenter:appcenter-analytics:${appCenterSdkVersion}"
    implementation "com.microsoft.appcenter:appcenter-crashes:${appCenterSdkVersion}"

    def room_version = "2.3.0"
    implementation "androidx.room:room-runtime:$room_version"
    kapt "androidx.room:room-compiler:$room_version"

    // optional - Kotlin Extensions and Coroutines support for Room
    implementation "androidx.room:room-ktx:$room_version"

    // optional - RxJava support for Room
    implementation "androidx.room:room-rxjava2:$room_version"

    def lifecycle_version = "2.3.1"
    def arch_version = "2.1.0"

    // ViewModel
    implementation("androidx.lifecycle:lifecycle-viewmodel-ktx:$lifecycle_version")
    // LiveData
    implementation("androidx.lifecycle:lifecycle-livedata-ktx:$lifecycle_version")
    // Lifecycles only (without ViewModel or LiveData)
    implementation("androidx.lifecycle:lifecycle-runtime-ktx:$lifecycle_version")
    implementation("androidx.lifecycle:lifecycle-reactivestreams-ktx:$lifecycle_version")

    def rx_life_cycle = "3.0.0"
    implementation "com.trello.rxlifecycle3:rxlifecycle:$rx_life_cycle"
    implementation "com.trello.rxlifecycle3:rxlifecycle-android-lifecycle-kotlin:$rx_life_cycle"

    testImplementation 'junit:junit:4.13.2'
    testImplementation "org.mockito:mockito-core:2.27.0"
}
